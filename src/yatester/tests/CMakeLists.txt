# Add yatester module
# add_yatester_selftest(source)
function(add_yatester_selftest source)
	get_filename_component(source_we ${source} NAME_WE)
	string(CONCAT name ${source_we} test)
	add_executable(${name} ${source})
	target_link_libraries(${name} yatester)
	set_target_properties(${name} PROPERTIES FOLDER tests)
	string(CONCAT script_regex ${source_we} ".*.script")
	file(GLOB SCRIPTS RELATIVE ${CMAKE_CURRENT_SOURCE_DIR} ${script_regex})
	if(SCRIPTS)
		add_tester_scripts(${name} SOURCES ${SCRIPTS})
	endif()
endfunction()

function(add_failing_test)
	set(multiValueArgs NAME COMMAND)
	cmake_parse_arguments(ARG "" "" "${multiValueArgs}" ${ARGN})
	if(NOT DEFINED ARG_NAME)
		message(FATAL_ERROR "NAME not defined")
	endif()
	if(NOT DEFINED ARG_COMMAND)
		message(FATAL_ERROR "COMMAND not defined")
	endif()
	add_test(NAME ${ARG_NAME} COMMAND ${ARG_COMMAND})
	set_tests_properties(${ARG_NAME} PROPERTIES WILL_FAIL TRUE)
endfunction()

add_yatester_selftest(empty.test.c)
add_failing_test(NAME empty.fail.0.script COMMAND emptytest "--list-commands")
add_failing_test(NAME empty.fail.1.script COMMAND emptytest "--input-file" "x")

if(CMAKE_BUILD_TYPE STREQUAL Debug)
	add_failing_test(NAME empty.fail.2.script COMMAND emptytest "--log-file" "/")
	add_failing_test(NAME empty.fail.3.script COMMAND emptytest "--enable-log-channel" "x")
	add_failing_test(NAME empty.fail.4.script COMMAND emptytest "--malloc-failing-rate" "x")
	add_failing_test(NAME empty.fail.5.script COMMAND emptytest "--malloc-failing-index" "x")
	add_failing_test(NAME empty.fail.6.script COMMAND emptytest "--prng-seed" "x")
endif()

add_yatester_selftest(arg.test.c)
